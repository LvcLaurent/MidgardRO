//===== rAthena Script ======================================= 
//= Sorcerer Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Sage / Professor -> Sorcerer.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Cleaning. [Euphy]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//============================================================

gef_tower,102,34,5	script	Merito	742,{
	mes "[Merito]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "Vous avez trop chargé votre sac. Essayez à nouveau après avoir vidé votre sac.";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "Veuillez bien vous entendre avec les esprits~ Vous devriez~!";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "Wow~ vous êtes un jeune aventurier. Je vous envie.";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "Ahh~ Je pense que vous n'avez pas besoin de parler avec moi.";
		close;
	}
	if (job_soc == 0) {
		mes "Bienvenue. Comment puis-je vous aider ?";
		next;
		select("Savez-vous quelque chose sur les Sorciers ?");
		mes "[Merito]";
		mes "Ne serait-il pas plus confortable de vivre simplement en tant qu'érudit ? Les Sorciers sont occupés à communiquer avec les esprits et n'ont pas beaucoup de temps pour lire des livres...";
		next;
		mes "[Merito]";
		mes "Qu'est-ce qui vous a poussé à devenir un sorcier ?";
		next;
		if(select("L'esprit du feu m'a conduit ici.:L'esprit de l'eau m'a conduit ici.:L'esprit de la terre m'a conduit ici:L'esprit de la viande m'a conduit ici.") == 4) {
			mes "[Merito]";
			mes "Ce genre d'esprit n'existe pas !";
			close;
		}
		mes "[Merito]";
		mes "Oh, je vois.";
		mes "Vous devez être né avec la capacité naturelle de voir que vous avez déjà communiqué avec les esprits, ou vous devez être un excellent menteur.";
		next;
		mes "[Merito]";
		mes "Hmm... Alors d'abord, pouvez-vous écrire votre nom sur cette liste ?";
		next;
		if(select("Écrire le nom.:Je ne veux pas.") == 2) {
			mes "[Merito]";
			mes "Eh bien, d'accord. À la prochaine fois, quand vous en aurez l'occasion.";
			close;
		}
		mes "[Merito]";
		mes "Donc, vous êtes [" + strcharinfo(0) + "].";
		next;
		mes "[Merito]";
		mes "Laissez-moi une seconde pour enregistrer votre nom.";
		mes "Reparlez-moi dans une seconde.";
		set job_soc,1;
		close;
	} else if (job_soc == 1) {
		mes "Bien, alors je vais vous donner une brève explication sur le Sorcier.";
		next;
		mes "[Merito]";
		mes "[" + strcharinfo(0) + "], pouvez-vous manipuler la magie avec habileté ?";
		next;
		if(select("Oui, je le peux.:Non, je ne suis pas vraiment doué pour ça.") == 2) {
			mes "[Merito]";
			mes "Quoi !? Vous ne savez pas utiliser la magie ?";
			mes "Qu'est-ce que vous faites ici alors ? Sortez d'ici. Tout de suite !";
			close;
		}
		mes "[Merito]";
		mes "D'accord, ça devrait être évident.";
		mes "Bien sûr, les Sorciers utilisent aussi la magie. Mais savez-vous quelle est la principale différence entre les Sorciers et les autres emplois magiques ?";
		next;
		switch(select("Ils savent comment chanter.:Ce sont tous des vieux schnocks.:Ils peuvent communiquer avec les esprits.")) {
		case 1:
			mes "[Merito]";
			mes "Eh bien, peut-être que certains, mais ce n'est pas la principale différence.";
			close;
		case 2:
			mes "[Merito]";
			mes "Quoi ! Comment osez-vous ! ?";
			close;
		case 3:
			break;
		}
		mes "[Merito]";
		mes "C'est exact. C'est la caractéristique la plus distinctive des Sorciers.";
		next;
		mes "[Merito]";
		mes "Quand les gens qui veulent devenir un Sorcier viennent, la première chose que nous faisons est de vérifier s'ils peuvent communiquer avec les esprits ou non.";
		next;
		mes "[Merito]";
		mes "Alors voilà pour l'introduction de base, donc vous n'avez qu'à décider si vous voulez passer le test de qualification ou non.";
		next;
		mes "[Merito]";
		mes "Eh bien, tout dépend de vous. Que comptez-vous faire ?";
		next;
		switch(select("Je vais passer le test.:Je le ferai plus tard.")) {
		case 1:
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_soc,2;
				setquest 12096;
				next;
			}
			mes "[Merito]";
			mes "D'accord. ";
			mes "Alors je vais vous confier à Karacas qui est au 4ème étage. Il vous guidera tout au long du processus du test.";
			set job_soc,2;
			setquest 12096;
			close;
		case 2:
			mes "[Merito]";
			mes "Quoi ? J'étais sûr que vous vouliez devenir un Sorcier.";
			close;
		}
	} else {
		mes "Et votre test ? Eh bien, Karacas est un peu paresseux, alors cela pourrait vous déranger un peu... kkk";
		close;
	}
}

gef_tower,113,161,5	script	Karacas	754,{
	mes "[Karacas]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "Oh, il semble que vous soyez trop lourd. Videz votre sac et revenez ensuite.";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "Vous avez l'air heureux. Passez un excellent voyage.";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "Y a-t-il quelque chose que je puisse faire pour vous ?";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "Je fais partie de l'association des Sorciers. Je ne pense pas que vous ayez affaire à moi.";
		close;
	}
	if (job_soc < 2) {
		mes "De plus en plus de personnes visitent la Tour de Geffen ces jours-ci.";
		close;
	} else if (job_soc == 2) {
		mes "....";
		next;
		select("Ah... excusez-moi~");
		mes "[Karacas]";
		mes "... (Z z z~)";
		next;
		select("Hm hm!!!~");
		mes "[Karacas]";
		mes "Aaaak!! ";
		mes "Oh, mon... ";
		next;
		mes "[Karacas]";
		mes "Où... d'où venez-vous ?";
		next;
		select("Merito m'a dit de vous rendre visite.");
		mes "[Karacas]";
		mes "Je comprends, donnez-moi un instant.";
		mes "La liste... est déjà arrivée.";
		next;
		mes "[Karacas]";
		mes "Donc, vous êtes [" + strcharinfo(0) + "]... n'est-ce pas ?";
		next;
		if(select("Oui, c'est moi.:Non, ce n'est pas moi !") == 2) {
			mes "[Karacas]";
			mes "Oh, il doit y avoir une erreur dans la liste, je pense. Reviendrez-vous plus tard ?";
			close;
		}
		mes "[Karacas]";
		mes "Avant de passer le test de Sorcier, laissez-moi vous donner les informations de base.";
		next;
		mes "[Karacas]";
		mes "La façon d'obtenir un billet d'enregistrement de Sorcier est vraiment simple. Vous devez simplement trouver un esprit de chaque type dispersé dans le monde, puis convertir leur pouvoir en une forme matérielle spécifique.";
		next;
		mes "[Karacas]";
		mes "Cela semble facile, n'est-ce pas ?";
		next;
		select("Ahh... monsieur. Est-ce vraiment possible ?");
		mes "[Karacas]";
		mes "Eh bien, il est impossible de rencontrer chaque esprit par vous-même. Vous devez vous entraîner très dur pour donner une forme aux esprits et les voir clairement à travers vos yeux.";
		next;
		mes "[Karacas]";
		mes "Donc, habituellement, nous effectuons une certaine parade de cérémonie vers l'endroit où nous pouvons ressentir les esprits.";
		next;
		mes "[Karacas]";
		mes "Je vais vous expliquer comment faire. Alors écoutez attentivement.";
		next;
		mes "[Karacas]";
		mes "En général, nous ne pouvons pas voir les esprits avec nos yeux, mais il existe un moyen de réaliser une partie des esprits grâce à un médium spécial.";
		next;
		mes "[Karacas]";
		mes "Par exemple, si vous visitez l'endroit où l'esprit du vent réside avec une certaine quantité de Vent de Verdure, la réaction de l'esprit est amplifiée.";
		next;
		mes "[Karacas]";
		mes "De même, si vous approchez l'endroit où l'esprit du feu semble résider tout en ayant une certaine quantité de Sang Rouge, l'esprit montrera une réaction contraire.";
		next;
		mes "[Karacas]";
		mes "Les Sorciers utilisent ce principe de base, c'est la connaissance essentielle que les Sorciers doivent avoir pour atteindre les esprits.";
		next;
		select("Donc, communiquer avec les esprits...");
		mes "[Karacas]";
		mes "C'est exact~";
		mes "Ce travail est basé sur le pouvoir des esprits.";
		next;
		mes "[Karacas]";
		mes "Hmm, mais cela pourrait être difficile pour quelqu'un comme vous qui se concentre uniquement sur la lecture de livres...";
		next;
		mes "[Karacas]";
		mes "Tout d'abord, vous devez apporter les matériaux nécessaires pour le test.";
		next;
		mes "[Karacas]";
		mes "3 Wind of Verdure.";
		mes "3 Crystal Blue.";
		mes "3 Red Blood.";
		mes "3 Green Live.";
		next;
		mes "[Karacas]";
		mes "Lorsque vous apporterez ces matériaux, je vous dirai la prochaine étape.";
		set job_soc,3;
		changequest 12096,12097;
		close;
	} else if (job_soc == 3) {
		if (countitem(992) > 2 && countitem(991) > 2 && countitem(990) > 2 && countitem(993) > 2) {
			mes "Oh, vous avez tous les matériaux.";
			next;
			select("Que dois-je faire ensuite ?");
			mes "[Karacas]";
			mes "Je vais vous en parler maintenant. Tout d'abord, je vais vous donner les endroits du test. Vous feriez bien de les noter.";
			next;
			mes "[Karacas]";
			mes "1. 1er étage de la Grotte de Glace";
			mes "2. 1er étage du Donjon du Volcan de Thor";
			mes "3. 3e étage du Mjolnir Dead Pit";
			mes "4. 2e étage du Donjon de Kunlun";
			next;
			mes "[Karacas]";
			mes "Nous avons installé des pôles de détection dans ces 4 endroits de manière à ce que les personnes ordinaires puissent communiquer avec les esprits.";
			next;
			mes "[Karacas]";
			mes "Sachez que ces pôles de détection ne sont activés que lorsque vous avez plus que la quantité minimale de pierres de propriété.";
			next;
			mes "[Karacas]";
			mes "Vous devez préparer au moins 3 petites pierres de chaque propriété. Par exemple, vous avez besoin de Vent de Verdure, pas de Vent Rugueux.";
			next;
			select("Donc j'ai besoin des pierres de propriété...");
			mes "[Karacas]";
			mes "Oui, vous avez de l'esprit !";
			next;
			mes "[Karacas]";
			mes "Une fois que vous apporterez les pierres aux pôles, mettez-les dans la machine de décomposition de propriété qui est attachée au pôle. Le pôle de détection sera activé, et vous pourrez communiquer avec l'esprit.";
			next;
			mes "[Karacas]";
			mes "Si vous réussissez, vous pourrez extraire une partie de l'esprit sous forme de réactif. Si vous échouez, la pierre de propriété se brisera.";
			next;
			mes "[Karacas]";
			mes "Bien sûr, si vous avez de la chance, en cas d'échec, un Vent de Verdure peut se transformer en Vent Rugueux... Eh bien, ce n'est pas une situation courante, alors laissons ça de côté pour l'instant.";
			next;
			select("Que se passe-t-il si je manque de pierres ?");
			mes "[Karacas]";
			mes "Ahahaha... Si vous manquez de pierres de propriété, vous devrez simplement en obtenir davantage.";
			next;
			mes "[Karacas]";
			mes "Donc, les personnes qui passent ce test commencent généralement avec autant de pierres de propriété qu'elles peuvent porter. La quantité que je vous ai conseillée est juste le strict minimum.";
			next;
			mes "[Karacas]";
			mes "Si vous êtes un Sorcier expérimenté, vous pourrez parler aux esprits sans passer à nouveau par ce processus. Mais les débutants doivent utiliser ce type de médium pour communiquer avec eux.";
			next;
			select("Donc, le réactif est... ?");
			mes "[Karacas]";
			mes "Nous ne savons pas quel réactif sera fabriqué à partir des esprits. Cela dépend de l'état des esprits.";
			next;
			mes "[Karacas]";
			mes "Lorsque vous réussirez à obtenir 3 types différents de réactifs, vous pourrez revenir.";
			next;
			mes "[Karacas]";
			mes "Un réactif fabriqué par un esprit ne sera pas de plus de 2 types, donc si vous voulez fabriquer 3 types de réactifs, je vous suggère de visiter plusieurs endroits.";
			next;
			mes "[Karacas]";
			mes "Avez-vous des questions ?";
			next;
			if(select("Non.:Veuillez expliquer à nouveau.") == 2) {
				mes "[Karacas]";
				mes "Haha, puis-je avoir une tasse d'eau pendant une minute ? Revenez me voir plus tard.";
				next;
			}
			mes "[Karacas]";
			mes "Alors je vais noter les informations que j'ai résumées brièvement. Attendez...";
			next;
			mes "^000099Vous avez obtenu un papier sur lequel est écrit la manière de communiquer avec les esprits. Pour plus d'informations, veuillez ouvrir votre fenêtre de quête^000000.";
			set job_soc,4;
			changequest 12097,12098;
			close;
		}
		mes "Avant de vous expliquer la suite du processus, vous devrez apporter les matériaux nécessaires.";
		next;
		mes "[Karacas]";
		mes "3 Wind of Verdure.";
		mes "3 Crystal Blue.";
		mes "3 Red Blood.";
		mes "3 Green Live.";
		next;
		mes "[Karacas]";
		mes "Lorsque vous apporterez ces éléments de base, je vous guiderai vers la prochaine étape.";
		close;
	} else if (job_soc == 4) {
		if (countitem(6276) && countitem(6278) && countitem(6277)) {
			mes "Wow~ vous avez de bonnes compétences.";
			mes "Je pensais que vous seriez submergé par les esprits, mais vous avez réussi à obtenir le remède efficace !";
			next;
			select("Est-ce tout ce que je dois faire ?");
			mes "[Karacas]";
			mes "Bien joué. Tout d'abord, je vais organiser le remède miraculeux, alors veuillez patienter un moment.";
			delitem 6276,1; //Clear_Reagent
			delitem 6278,1; //Black_Reagent
			delitem 6277,1; //Red_Reagent
			set job_soc,5;
			completequest 12098;
			close;
		}
		mes "Ce n'est pas facile d'obtenir l'effet désiré en communiquant avec les esprits.";
		next;
		mes "[Karacas]";
		mes "Ne baissez pas les bras et continuez à essayer~";
		close;
	} else if (job_soc == 5) {
		mes "À l'origine, mon maître devait vérifier le changement de classe de Sorcier, mais après être parti chercher l'esprit du poisson, je ne peux plus le voir. Alors je vais le vérifier moi-même.";
		next;
		mes "[Karacas]";
		mes "Cher [" + strcharinfo(0) + "],";
		mes "vous avez parcouru les terres sauvages pour ouvrir la porte en tant que Sorcier et vous avez satisfait à toutes les exigences de notre Académie.";
		next;
		mes "[Karacas]";
		mes "Bien sûr, ce n'est que le début, nous ne savons pas quel esprit vous allez rencontrer ou quelles épreuves vous attendent.";
		next;
		mes "[Karacas]";
		mes "Mais avec les compétences que vous avez montrées jusqu'à présent, vous serez capable de tout à partir de maintenant. Cher [" + strcharinfo(0) + "]";
		next;
		mes "[Kalakas]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint) {
			mes "Avant que je vous attribue le nouveau job, veuillez vérifier votre état corporel par vous-même. ";
			next;
			mes "[Kalakas]";
			mes "Vous devez avoir l'étiquette de votre job maintenant avant d'accepter le nouveau job ! ";
			close;
		}
		if (ismounting()) {
			mes "De plus, avant de commencer...";
			mes "Veuillez déséquiper votre monture~";
			close;
		}
		mes "De plus, je reconnais formellement que vous avez réussi tous les tests de changement de job de l'Académie des Sorciers.";
		next;
		mes "[Karacas]";
		mes "Félicitations, [" + strcharinfo(0) + "]";
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5756,1; //Wind_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[Karacas]";
		mes "J'espère que vous gagnerez encore plus de communion avec les esprits à partir de maintenant.";
		close;
}
}
-	script	Spirit Detecting Staff#0	-1,{

	if (strnpcinfo(4) == "thor_v01") setarray .@id[0],990,994,6276,6278;
	else if (strnpcinfo(4) == "ice_dun01") setarray .@id[0],991,995,6278,6276;
	else if (strnpcinfo(4) == "mjo_dun03") setarray .@id[0],993,997,6277,6276;
	else if (strnpcinfo(4) == "gon_dun02") setarray .@id[0],992,996,6277,6278;

	if (job_soc == 4 && countitem(.@id[0]) > 2) {
		progressbar "ffff00",5;
		delitem .@id[0],3; //Boody_Red,Crystal_Blue,Yellow_Live,Wind_Of_Verdure
		set .@soc_ran, rand(1,70);
		if (.@soc_ran < 10) {
			if (countitem(.@id[2]) == 0) {
				mes "Vous avez obtenu un réactif en communiquant avec un esprit invisible.";
				getitem .@id[2],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "Il ne semble pas que vous ayez obtenu quelque chose de spécial des esprits.";
			close;
		} else if (.@soc_ran > 9 && .@soc_ran < 16) {
			if (countitem(.@id[3]) == 0) {
				mes "Vous avez obtenu un réactif en communiquant avec un esprit invisible.";
				getitem .@id[3],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "Il ne semble pas que vous ayez obtenu quelque chose de spécial des esprits.";
			close;
		} else if (.@soc_ran == 56) {
			mes "L'esprit a combiné les pierres de propriété en une énorme pierre de propriété.";
			getitem .@id[1],1; //Flame_Heart,Mistic_Frozen,Great_Nature,Rough_Wind
			close;
		} else {
			mes "Vous avez utilisé les pierres de propriété, mais il semble que vous ayez échoué à communiquer avec les esprits.";
			close;
		}
	}
	mes "Je peux voir le personnel de détection qui a été installé par l'Union des Sorciers pour communiquer avec les esprits.";
	if (job_soc == 4 && countitem(.@id[0]) < 3) {
		next;
		mes "Si vous avez les pierres de propriété appropriées, vous devriez pouvoir communiquer avec les esprits.";
	}
	close;
}
thor_v01,64,252,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#1	836
ice_dun01,274,274,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#2	836
mjo_dun03,200,141,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#3	836
gon_dun02,195,190,0	duplicate(Spirit Detecting Staff#0)	Spirit Detecting Staff#4	836
