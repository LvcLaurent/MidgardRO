//===== rAthena Script ======================================= 
//= Sura Job Quest
//===== By: ================================================== 
//= Muad_Dib, Gepard
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Monk / Champion -> Sura.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Removed disablenpc which causes players stucked in waitingroom. [Joseph]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added GM management function. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//= 1.5 Fixed players stuck in waiting room, event triggers properly [Luciar]
//============================================================

ve_in,237,125,0	script	King Crab#job_shu	107,{
	if (job_shu == 0) {
		if (BaseJob == Job_Monk && BaseLevel >= 99 && JobLevel >= 50) {
			set .@job_check,1;
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_shu,1;
				setquest 11155;
				next;
			}
		}
		mes "[King Crab]";
		mes "Khh ha ha ha ha ha ha.";
		mes "Ce petit bleu s'est fait assommer d'un seul coup!";
		next;
		mes "[Sludge Worm]";
		mes "Ces gars arrogants méritent une leçon amère.";
		mes "Comment ose un petit rookie vous attaquer, boss!";
		next;
		if (!.@job_check) {
			mes "[King Crab]";
			mes "Khhha ha ha ha ha ha!!!!";
			mes "Une autre bière, ici!!!";
			mes "Je vais boire aujourd'hui!!!";
			next;
			mes "[Sludge Worm]";
			mes "King Crab!!! Je te suivrai toujours!!";
			close;
		}
		mes "[King Crab]";
		mes "Ha ha ha ha ha ha ha!!!!";
		next;
		mes "[Sludge Worm]";
		mes "Hé! Ici!!! Encore une bière!!!";
		mes "Qu'est-ce que tu fais? La chope de mon boss est déjà vide! Dépêche-toi!";
		next;
		mes "[Waitress]";
		mes "Oui, je vais l'apporter tout de suite.";
		next;
		mes "[Sludge Worm]";
		mes "Trop lent!!! Pour qui tu me prends? Hein!?";
		next;
		mes "[Waitress]";
		mes "J... Je suis vraiment désolée, monsieur...";
		next;
		mes "[King Crab]";
		mes "K k k k k. C'est assez.";
		mes "Cette jolie fille semble avoir peur de toi!";
		mes "Hé, mademoiselle~ Nous ne sommes pas des gens effrayants~~";
		next;
		mes "[Waitress]";
		mes "Aaaaaaaaaaak! Ne faites pas ça!!";
		next;
		mes "- Bang -";
		donpcevent "???#bcmd::OnEnable";
		next;
		mes "[???]";
		mes "Eh bien, bonjour.";
		mes "Je vous ai observé, faire du bruit comme si vous étiez chez vous.";
		next;
		mes "[???]";
		mes "C'est trop bruyant. Alors si vous voulez dire des bêtises, rentrez chez vous.";
		next;
		mes "[Sludge Worm]";
		mes "Quoi?!";
		mes "Je ne sais pas qui tu es mais tu as dû perdre la tête!";
		mes "Tu as une idée de qui est mon boss?!!";
		next;
		mes "[Sludge Worm]";
		mes "C'est le célèbre King Crab!!!";
		mes "Qui peut décimer des armées entières avec son poing!!!";
		next;
		mes "[???]";
		mes "Tu fais toutes les choses sales, comme ton visage sale.";
		mes "Tu n'es qu'un voyou satisfaisant tes désirs.";
		next;
		mes "[King Crab]";
		mes "Quoi?";
		mes "Comment oses-tu dire de telles choses?";
		mes "Ce n'est pas parce que tu es une fille que je vais laisser passer.";
		next;
		mes "[Sludge Worm]";
		mes "Huh! Le boss est vraiment en colère!";
		mes "Je peux te battre d'un coup...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Hé! Comment oses-tu utiliser la violence contre une dame!?";
		next;
		mes "[???]";
		mes "Recule, petite chose...";
		next;
		mes "[???]";
		mes "Oh, je suis contente d'entendre ça.";
		mes "Je n'attaquerai pas en premier car je suis en plein entraînement.";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "Quoi?";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "Uhhhh?!";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "Aaaaaahhhhhhhhk!!!!!!!!";
		specialeffect EF_FIRESPLASHHIT,AREA,"King Crab#job_shu";
		specialeffect EF_FIRESPLASHHIT,AREA,"Sludge Worm#job_shu";
		next;
		mes "- Quelle puissance incroyable! -";
		mes "- Je n'ai jamais vu ça -";
		mes "- de compétence auparavant... -";
		next;
		mes "[???]";
		mes "Eh bien, l'ambiance est gâchée...";
		next;
		mes "[Waitress]";
		mes "M... Merci beaucoup...";
		mes "Comment puis-je vous remercier?";
		next;
		mes "[???]";
		mes "Ce n'est pas grand-chose. C'est bien.";
		next;
		mes "[Waitress]";
		mes "Rentrez-vous à votre logement?";
		mes "Si cela vous convient, puis-je vous apporter un repas?";
		next;
		mes "[???]";
		mes "Oh, ce n'est vraiment pas nécessaire.";
		mes "Juste une tranche de pain et une soupe chaude me suffiront.";
		mes "Ensuite, je dois dire au revoir. Salut.";
		donpcevent "???#bcmd::OnDisable";
		next;
		mes "[Waitress]";
		mes "Au revoir...";
		mes "...";
		mes ".....";
		mes "........(rouge de gêne)";
		set job_shu,1;
		setquest 11155;
		close;
	} else {
		if (strnpcinfo(0) == "King Crab#job_shu") {
			mes "[King Crab]";
			mes "Comment se fait-il qu'un petit rookie m'ait assommé! Moi! Le grand King Crab...";
		} else {
			mes "[Sludge Worm]";
			mes "Boss... Sommes-nous vraiment passés de notre apogée maintenant?";
			mes "Sanglots... sanglots...";
		}
		close;
	}
}
ve_in,241,128,4	duplicate(King Crab#job_shu)	Sludge Worm#job_shu	110

ve_in,244,126,3	script	Waitress#job_shu	69,{
	mes "[Waitress]";
	if (job_shu == 0) {
		mes "Oh non~";
		mes "Je déteste ces clients bruyants et impolis!";
		mes "J'espère qu'il y a quelqu'un qui peut les réprimander pour moi.";
		close;
	} else if (job_shu == 1) {
		if (BaseJob == Job_Monk) {
			mes "Oh chère~";
			mes "Je vous suis vraiment reconnaissante, "+jobname(Class)+", de m'avoir aidée tout à l'heure.";
			next;
		}
		mes "Ah, la personne que nous avons vue auparavant?";
		mes "Elle est restée dans ce village pendant des jours,";
		mes "N'est-elle pas formidable?";
		next;
		mes "[Waitress]";
		mes "Bien qu'elle soit une femme comme moi, elle était si géniale!";
		mes "Aaahh~ Cette belle et imposante silhouette...";
		next;
		mes "[Waitress]";
		mes "Oh là là!!";
		mes "J'ai oublié de faire quelque chose!";
		mes "Je devrais cuisiner tout de suite et ensuite apporter le repas à l'auberge. Aaahh~";
		next;
		mes "[Waitress]";
		mes "Si vous ne savez toujours pas où loger, veuillez utiliser ^f57d7d l'auberge du côté supérieur de ce bâtiment^000000.";
		close;
	}
	mes "Maintenant que les clients se sont un peu calmés.";
	mes "Ahhhh, je me sens mieux maintenant!!";
	close;
}

ve_in,240,131,0	script	???#bcmd	484,{
	end;
OnInit:
	disablenpc "???#bcmd";
	end;
OnEnable:
	enablenpc "???#bcmd";
	initnpctimer;
	end;
OnDisable:
	disablenpc "???#bcmd";
	stopnpctimer;
	end;
OnTimer600000:
	donpcevent "???#bcmd::OnDisable";
	stopnpctimer;
	end;
}

ve_in,97,149,0	script	#Suraryokan	139,3,3,{
OnTouch:
	if (job_shu == 1) {
		set .@n$, "["+strcharinfo(0)+"]";
		mes "[???]";
		mes "Qui est-ce ?";
		next;
		mes .@n$;
		mes "Je suis la personne qui était au pub il y a un moment.";
		mes "J'ai quelque chose à vous demander.";
		next;
		mes "[???]";
		mes "Ne me dérangez pas, partez simplement !";
		next;
		mes .@n$;
		mes "S'il vous plaît, je veux juste parler.";
		next;
		mes "[???]";
		mes "...";
		next;
		mes .@n$;
		mes "Ne m'ignorez pas, parlez simplement avec moi.";
		next;
		mes .@n$;
		mes "Juste pour un instant, ce n'est pas grave ?";
		next;
		mes .@n$;
		mes "Je ne suis pas là pour vous faire du mal.";
		next;
		mes .@n$;
		mes "Comment pourriez-vous maltraiter quelqu'un qui est venu frapper à votre porte ?";
		next;
		mes .@n$;
		mes "Vous allez trop loin.";
		next;
		mes .@n$;
		mes "Après vous avoir vu combattre...";
		next;
		mes .@n$;
		mes "J'ai pensé que vous deviez pratiquer les arts martiaux et je suis venu ici pour vous rencontrer.";
		next;
		mes .@n$;
		mes "Mais en voyant que vous me laissez dehors comme ça...";
		next;
		mes .@n$;
		mes "Vous devez être avancé dans les arts martiaux...";
		next;
		mes "[???]";
		mes "Comme vous êtes persévérant...";
		next;
		mes "[???]";
		mes "Que diable voulez-vous ?";
		set job_shu,2;
		close;
	}
	end;
}

ve_in,98,159,0	script	Fighter#job_shu	484,{
	if (job_shu < 2) {
		mes "[Fighter]";
		mes "Ahhhh~ Je suis tellement fatigué.";
		mes "L'entraînement est si dur et j'ai encore un long chemin à parcourir !";
		close;
	} else if (job_shu == 2) {
		mes "["+strcharinfo(0)+"]";
		mes "Je veux en savoir plus sur la compétence que vous avez utilisée tout à l'heure.";
		next;
		mes "[???]";
		mes "...";
		mes "......";
		mes ".........";
		mes "............ Un novice comme vous n'a pas besoin de savoir.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Novice ?!";
		mes "J'ai pratiqué assidûment les arts martiaux.";
		mes "Je suis confiant que je ne perdrai jamais.";
		next;
		mes "[???]";
		mes "Alors vous pouvez aussi me battre ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Non...";
		mes "Je suppose que c'est trop...";
		mes "Franchement, j'ai été assez impressionné par vous.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je pensais être assez fort, mais après vous avoir vu combattre, j'ai réalisé que j'étais plein de suffisance.";
		next;
		mes "[???]";
		mes "Huuuh~ Peu importe à quel point vous êtes fort,";
		mes "vous ne pouvez pas éviter d'atteindre votre véritable limite.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Vous êtes juste la personne que j'attendais.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Je pense que vous sauriez";
		mes "comment surmonter cette limitation.";
		next;
		mes "[???]";
		mes "Arrêtez ça.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Quoi ? Pourquoi ?";
		next;
		mes "[???]";
		mes "Êtes-vous humain ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Quoi ?";
		mes "Bien sûr, je suis humain.";
		next;
		mes "[???]";
		mes "Devenir plus fort est trop difficile pour un simple humain.";
		mes "Si vous le souhaitez, achetez simplement de meilleurs équipements.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Alors, qu'êtes-vous ?";
		mes "Vous voulez dire que vous n'êtes pas un être humain ?";
		next;
		mes "[???]";
		mes "Dépasser la limite...";
		mes "est impossible pour un être humain.";
		mes "C'est la voie d'un fantôme...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Fantôme ?";
		next;
		mes "[???]";
		mes "J'ai renoncé à être un être humain après avoir choisi cette voie.";
		mes "La voie pour devenir un Sura en abandonnant la protection divine.";
		next;
		mes "[???]";
		mes "Vous n'aurez même pas le temps de vous reposer si vous choisissez cette voie.";
		mes "Vous deviendrez une marionnette de guerre...";
		next;
		switch(select("Mais je veux toujours en savoir plus.:Eh bien, alors j'abandonne.")) {
			case 1:
				mes "[???]";
				mes "N'êtes-vous qu'un idiot ?";
				mes "Pourquoi êtes-vous si heureux de devenir un fantôme ?";
				next;
				mes "[???]";
				mes "Vous le regretterez, alors contentez-vous de votre vie actuelle.";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "Eh bien, maintenant que je vous ai rencontré, je ne vivrai plus ma vie de cette manière !";
				next;
				mes "[???]";
				mes "Hah... vous me fatiguez vraiment.";
				next;
				mes "[???]";
				mes "D'accord, alors ^f57d7dallez tuer 100 Loups du Désert.^000000";
				mes "Après cela, j'y réfléchirai à nouveau.";
				set job_shu,3;
				changequest 11155,11156;
				close;
			case 2:
				mes "[???]";
				mes "Eh bien, bon choix.";
				mes "Vous n'avez pas besoin de chercher les ennuis.";
				close;
		}
	} else if (job_shu == 3) {
		if (checkquest(11156,HUNTING) == 2) {
			mes "[???]";
			mes "Wow~";
			mes "Avez-vous vraiment tué tous ces loups ?";
			mes "Je pensais que vous auriez abandonné.";
			next;
			mes "[???]";
			mes "La parole d'une femme est sacrée !!";
			mes "Une promesse est une promesse.";
			next;
			mes "[???]";
			mes "Allez à ^f57d7dEl Mes Gorge, au sud-est de Juno^000000.";
			mes "Mon collègue ^f57d7d'Buddy'^000000 s'entraînera près du centre de cette zone.";
			next;
			mes "[Bruno]";
			mes "Dites-lui que 'Bruno' vous a envoyé là-bas, puis faites ce que vous voulez.";
			set job_shu,4;
			changequest 11156,11157;
			close;
		}
		mes "[???]";
		mes "Hah... vous me fatiguez vraiment.";
		next;
		mes "[???]";
		mes "D'accord, alors ^f57d7dallez tuer 100 Loups du Désert.^000000";
		mes "Ensuite, j'y réfléchirai à nouveau.";
		close;
	} else if (job_shu == 4) {
		mes "[Bruno]";
		mes "Allez à ^f57d7dEl Mes Gorge, au sud-est de Juno^000000.";
		mes "Mon collègue ^f57d7d'Buddy'^000000 s'entraînera près du centre de cette zone.";
		next;
		mes "[Bruno]";
		mes "Dites-lui que 'Bruno' vous a envoyé là-bas, puis faites ce que vous voulez.";
		close;
	} else if (job_shu == 100) {
		mes "[Bruno]";
		mes "Hé~ !";
		mes "Comment ça va ?";
		next;
		mes "[Bruno]";
		mes "Vous n'avez pas de cadeaux pour votre professeur ?";
		close;
	}
	mes "[Bruno]";
	mes "Je vous recommande de réfléchir à nouveau à devenir un Sura.";
	mes "Sura est fort et cool, mais ce n'est pas tout.";
	next;
	mes "[Bruno]";
	mes "Moi ?";
	mes "Si j'ai besoin de choisir un métier à nouveau, je choisirai certainement Sura.";
	close;
}

yuno_fild07,254,176,7	script	Buddy#job_shu	753,{
	if (job_shu < 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "Je ne sais pas pourquoi tu es ici, mais s'il te plaît, ne te tiens pas derrière moi à moins que tu ne veuilles mourir.";
		close;
	} else if (job_shu == 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "Je ne sais pas pourquoi tu es ici, mais s'il te plaît, ne te tiens pas derrière moi à moins que tu ne veuilles mourir.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "'Bruno' m'a envoyé ici.";
		next;
		mes "[Buddy]";
		mes "Ah ! Mon collègue m'a parlé de toi !";
		mes "De toute façon, pourquoi es-tu ici pour me voir ?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "J'ai demandé à Bruno à propos de sa force, et elle m'a recommandé de venir te voir.";
		next;
		mes "[Buddy]";
		mes "Cette fille difficile... non, je plaisante.";
		mes "Tu dois être une personne spéciale pour qu'elle t'envoie me voir.";
		next;
		mes "[Buddy]";
		mes "Eh bien, je ne peux pas répondre à ta question car je suis en train de m'entraîner, donc je vais te conduire chez notre maître.";
		next;
		mes "[Buddy]";
		mes "Mon maître n'aime pas le bruit, alors s'il te plaît, ^f57d7dattends en silence dans le salon.^000000";
		next;
		mes "[Buddy]";
		mes "Par ici...";
		set job_shu,5;
		changequest 11157,11158;
		close2;
		warp "sword_1-1",215,244;
		end;
	} else if (job_shu == 100) {
		mes "[Buddy]";
		mes "Ah, ça fait longtemps, "+strcharinfo(0)+".";
		mes "Es-tu ici pour rencontrer mon maître ?";
		next;
		switch(select("Oui:Non")) {
			case 1:
				mes "[Buddy]";
				mes "Je t'y emmènerai tout de suite.";
				close2;
				warp "sword_1-1",216,168;
				end;
			case 2:
				mes "[Buddy]";
				mes "Alors, pourquoi es-tu ici ?";
				mes "Tu n'es pas ici pour me voir, n'est-ce pas ?";
				close;
		}
	}
	mes "[Buddy]";
	mes strcharinfo(0)+", tu n'as pas encore rencontré le maître ?";
	next;
	mes "[Buddy]";
	mes "Mon maître n'aime pas le bruit, alors s'il te plaît, ^f57d7dattends en silence dans le salon.^000000";
	next;
	mes "[Buddy]";
	mes "Par ici...";
	close2;
	warp "sword_1-1",215,244;
	end;
}

sword_1-1,223,243,4	script	Drawing Room	753,{
	end;
OnInit:
	waitingroom "Drawing Room",20,"Drawing Room::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "sword_2-1",223,205;
	donpcevent "Buddy#Sura_Salon::OnEnable";
	disablewaitingroomevent;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

sword_1-1,223,243,4	script	Buddy#job_shu_Salong	753,{
	mes "[Buddy]";
	mes "Mon maître n'aime pas le bruit, alors s'il te plaît, ^f57d7dattends en silence dans le salon.^000000";
	next;
	switch(select("Aller dans le salon. : Sortir à l'extérieur.")) {
		case 1:
			mes "[Buddy]";
			mes "S'il te plaît, attends dans le salon du côté supérieur.";
			close;
		case 2:
			mes "[Buddy]";
			mes "Eh bien, à la prochaine.";
			close2;
			warp "yuno_fild07",255,178;
			end;
	}
}

sword_2-1,1,1,0	script	Buddy#Sura_Salon	66,{
	end;
OnInit:
	disablenpc "Buddy#Sura_Salon";
	end;
OnEnable:
	enablenpc "Buddy#Sura_Salon";
	initnpctimer;
	end;
OnReset:
	killmonster "sword_2-1","Buddy#Sura_Salon::OnMyMobDead";
	end;
OnDisable:
	disablenpc "Buddy#Sura_Salon";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("sword_2-1","Buddy#Sura_Salon::OnMyMobDead") < 1) {
		monster "sword_2-1",219,210,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	}
	end;
OnTimer3000:
	mapannounce "sword_2-1","Le maître est maintenant en plein entraînement et vous devez attendre dans le salon environ 5 minutes.",bc_map;
	end;
OnTimer4000:
	mapannounce "sword_2-1","Ah ! Vous pourriez avoir des hallucinations dans le salon, alors soyez prudent.",bc_map;
	end;
OnTimer5000:
	mapannounce "sword_2-1","C'est la politique du maître, alors ne vous sentez pas désagréable.",bc_map;
	end;
OnTimer33000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"It can't be true",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Illusion",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer93000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Just Imagination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer153000:
	mapannounce "sword_2-1","If you're bored, may I bring you some magazines?",bc_map;
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer213000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Never mind",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Can you see me?",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer273000:
	monster "sword_2-1",219,210,"",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer300000:
	mapannounce "sword_2-1","Le maître est arrivé. Bientôt, je vous montrerai la chambre du maître.",bc_map;
	end;
OnTimer303000:
	donpcevent "Buddy#job_shuaneh::OnEnable";
	end;
OnTimer305000:
	donpcevent "Buddy#Sura_Salon::OnReset";
	end;
OnTimer315000:
	mapannounce "sword_2-1","Viens pars ici.",bc_map;
	donpcevent "Buddy#job_shuaneh::OnDisable";
	donpcevent "#Sura_garajjom::OnEnable";
	end;
OnTimer320000:
	mapannounce "sword_2-1","Je suppose que vous n'êtes pas encore prêt à rencontrer le maître...",bc_map;
	mapwarp "sword_2-1","yuno_fild07",255,178;
	donpcevent "#Sura_garajjom::OnDisable";
	donpcevent "Buddy#Sura_Salon::OnDisable";
	donpcevent "Drawing Room::OnEnable";
	end;
}

sword_2-1,223,205,7	script	Buddy#job_shuaneh	753,{
	if (job_shu > 4) {
		mes "Je vais te montrer le chemin. Par ici.";
		close2;
		warp "sword_1-1",216,168;
		donpcevent "Buddy#job_shuaneh::OnDisable";
		end;
	}
	mes "Pourquoi es-tu ici ?";
	close2;
	warp "yuno_fild07",248,179;
	end;
OnInit:
OnDisable:
	disablenpc "Buddy#job_shuaneh";
	end;
OnEnable:
	enablenpc "Buddy#job_shuaneh";
	end;
}

sword_2-1,223,205,0	script	#Sura_garajjom	139,10,10,{
OnTouch_:
	warp "sword_1-1",216,168;
	end;
OnInit:
OnDisable:
	disablenpc "#Sura_garajjom";
	end;
OnEnable:
	enablenpc "#Sura_garajjom";
	end;
}

sword_1-1,222,169,5	script	Bruno#job_shu	484,{
	if (job_shu > 4) {
		mes "[Bruno]";
		mes "Hé~ "+strcharinfo(0)+", quoi de neuf ?";
		next;
		switch(select("Prenons une conversation.:Sortons d'ici.")) {
		case 1:
			if (job_shu == 100) {
				mes "[Bruno]";
				mes "Mon maître ?";
				mes "Il a dit qu'il avait chassé le monde de son esprit et qu'il était devenu un fantôme ayant renoncé à être humain, mais il semble avoir un désir persistant pour le monde.";
				next;
				mes "[Bruno]";
				mes "Eh bien, je suis aussi un Sura, mais j'aime toujours voyager et rencontrer beaucoup de gens~";
				next;
				mes "[Bruno]";
				mes "Autrefois, mon maître avait un ami proche, mais après être devenu un Sura,";
				next;
				mes "[Bruno]";
				mes "J'ai entendu dire qu'il avait écrit une lettre à son ami en disant qu'il était mort.";
				mes "Puis il visite parfois 'le lieu du souvenir' en secret.";
				next;
				mes "[Bruno]";
				mes "Il vaudrait mieux ne pas dire qu'il est mort et continuer à rencontrer son ami.";
				mes "Il est un peu têtu.";
				next;
				mes "[Bruno]";
				mes "Mais il est vraiment cool et impressionnant.";
				mes "Ho ho.";
				next;
				mes "[Bruno]";
				mes "La raison pour laquelle je continue de voyager est de trouver 'l'ami précieux' de mon maître et non de m'entraîner davantage.";
				next;
				mes "[Bruno]";
				mes "J'ai le cœur lourd quand je vois mon maître en train de regretter son ami...";
				mes "Ce n'est pas... pas seulement pour mon... maître !";
				close;
			}
			mes "[Bruno]";
			mes "Wow~ Je pensais que tu serais K.O. avant de voir mon maître, mais te voilà enfin ici ?";
			next;
			mes "[Bruno]";
			mes "Quoi qu'il en soit, tu es ici maintenant, donc je suis ton supérieur immédiat.";
			next;
			mes "[Bruno]";
			mes "Nous ne nous soucions pas de ton âge dans notre monde !";
			close;
		case 2:
			mes "[Bruno]";
			mes "Reviens quand tu veux~";
			mes "Je te montrerai le parcours d'entraînement des morts~";
			close2;
			warp "yuno_fild07",255,178;
			end;
		}
	}
	mes "[Bruno]";
	mes "Que fais-tu ici ?";
	mes "Comment as-tu trouvé cet endroit !";
	mes "Va-t'en !!!";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_1-1,223,167,2	script	Master#job_shu	483,{
	mes "[Maître]";
	if (job_shu == 5) {
		mes "Tu dois avoir de bons yeux pour voir que tu es enfin arrivé dans cette pièce.";
		next;
		mes "[Maître]";
		if (SkillPoint != 0) {
			mes "Tu ne peux pas devenir un Sura tant que tu n'as pas utilisé tous tes points de compétence.";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "Tu sembles être lourdement chargé. Veuillez enlever une partie de ta charge.";
			close;
		}
		if (BaseLevel < 99 || JobLevel < 50) {
			mes "Tu ne sembles pas avoir atteint la limite d'un être humain, tu as encore beaucoup de chemin à parcourir pour devenir Sura.";
			mes "Veuillez aller t'entraîner davantage.";
			close;
		}
		if (BaseJob != Job_Monk) {
			mes "Hmm ?";
			close;
		}
		mes "Bien sûr~";
		mes "J'ai des yeux perçants.";
		next;
		mes "[Bruno]";
		mes "Maître ! Voici un manteau de fourrure de loup.";
		mes "Mets-le quand il fait froid.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ahhk ! C'est le loup que j'ai... attrapé... Uhh.";
		next;
		mes "- Paf -";
		next;
		mes "[Bruno]";
		mes "Ho ho ho~";
		mes "As-tu quelque chose à dire, "+strcharinfo(0)+" ?";
		mes "Tu es maintenant un membre de notre famille, vivons en harmonie ~";
		next;
		mes "[Maître]";
		mes "D'accord, je t'accepte maintenant comme mon élève, aussi.";
		next;
		mes "[Maître]";
		mes "Je pense que tu le sais déjà, mais vivre en tant que Sura ne sera pas facile.";
		next;
		mes "[Maître]";
		mes "Je me suis également coupé du monde. Si un jour nous retournons dans le monde, ce doit être le jour où le monde aura besoin de fantômes pour les guerres.";
		next;
		mes "[Maître]";
		mes "J'espère que ces jours n'arriveront pas, mais... je serai volontiers le fantôme et tuerai tous les ennemis.";
		next;
		if (ismounting()) {
			mes "[Maître]";
			mes "Alors... descends de ta monture !";
			mes "C'est un manque d'étiquette dans cette situation.";
			close;
		}
		mes "[Maître]";
		mes "Tu devrais t'entraîner régulièrement jusqu'à ce jour-là.";
		set job_shu,100;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11158;
		getitem 5754,1; //Blazing_Soul
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_shu > 5) {
		mes "Un jour, j'étais aussi un jeune homme ordinaire qui voyageait beaucoup et rencontrait de nombreux amis.";
		next;
		mes "[Maître]";
		mes "Cependant, comme tu l'as peut-être ressenti, j'ai finalement atteint ma limite.";
		next;
		mes "[Maître]";
		mes "Parfois, je me demandais 'devrais-je tout abandonner pour être plus puissant...?'";
		next;
		mes "[Maître]";
		mes "Mais ce qui est fait est fait...";
		close;
	}
	mes "Ce n'est pas un endroit où tu peux rester.";
	mes "S'il te plaît, rentre chez toi.";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_2-1,1,2,0	script	Button Girl#sura	66,{
	callfunc "F_GM_NPC";
	switch(select("Activer l'arène.:Désactiver l'arène.:Activer le salon.:Activer Buddy.:Activer Gara.:Désactiver tout.:Fermer")) {
		case 1:
			mes "Activer l'arène.";
			donpcevent "Drawing Room::OnEnable";
			close;
		case 2:
			mes "Désactiver l'arène.";
			donpcevent "Drawing Room::OnDisable";
			close;
		case 3:
			mes "Activer le salon.";
			donpcevent "Buddy#Sura_Salon::OnEnable";
			close;
		case 4:
			mes "Activer Buddy.";
			donpcevent "Buddy#job_shuaneh::OnEnable";
			close;
		case 5:
			mes "Activer Gara.";
			donpcevent "#Sura_garajjom::OnEnable";
			close;
		case 6:
			mes "Désactiver tout.";
			donpcevent "Drawing Room::OnDisable";
			donpcevent "Buddy#Sura_Salon::OnReset";
			donpcevent "Buddy#Sura_Salon::OnDisable";
			donpcevent "Buddy#job_shuaneh::OnDisable";
			donpcevent "#Sura_garajjom::OnDisable";
			close;
		case 7:
			close;
	}
}
