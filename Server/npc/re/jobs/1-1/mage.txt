//===== rAthena Script ======================================= 
//= Renewal Mage Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Mage Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
geffen_in,164,124,4	script	Mage Guildsman#mage	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Homme de la Guilde des Mages]";
				mes "Whoa, ça fait longtemps ! Mais tu n'étais pas censé être mort ?";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "Ah, tu dois avoir été réincarné. Eh bien, je suis content de te retrouver.";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Homme de la Guilde des Mages]";
					mes "Désolé, mais je ne pense pas que tu sois prêt à apprendre la magie. Pourquoi n'irais-tu pas finir d'apprendre les Compétences de Base d'abord ?";
					next;
					mes "[Homme de la Guilde des Mages]";
					mes "Prends ton temps. Plus tu apprends, plus tu seras prêt à réapprendre la magie.";
					close;
				}
				mes "[Homme de la Guilde des Mages]";
				mes "Bien, puisque tu as déjà passé le test de Mage une fois, je ne remets pas en question ta qualification. Tu veux récupérer immédiatement tes compétences magiques, n'est-ce pas ?";
				next;
				skill "NV_TRICKDEAD", 0, SKILL_PERM;
				jobchange Job_Mage_High;
				atcommand "@joblvlup 5";
				skill "MG_ENERGYCOAT", 1, SKILL_PERM;
				mes "[Homme de la Guilde des Mages]";
				mes "Wow, pour une raison quelconque, tu as l'air beaucoup mieux qu'avant. De toute façon, je crois que tu feras un meilleur travail en tant que Mage aussi.";
				close;
			} else {
				mes "[Homme de la Guilde des Mages]";
				mes "Y a-t-il quelque chose de plus que je puisse faire pour toi ? Sinon, pourquoi n'irais-tu pas tester tes compétences ? Le monde t'attend~!";
				close;
			}
		} else {
			mes "[Homme de la Guilde des Mages]";
			mes "Quoi, tu t'intéresses à la guilde des Mages ? Je ne voulais pas te dire ça, mais tu n'as pas ta place ici.";
			next;
			mes "[Homme de la Guilde des Mages]";
			mes "Je ne suis pas sûr pourquoi tu es toujours debout devant moi, mais je peux dire que tu n'es pas destiné à être un Mage.";
			close;
		}
	}
	mes "[Homme de la Guilde des Mages]";
	if (BaseJob != Job_Novice) {
		if (BaseJob == Job_Mage) {
			mes "Hé, tu n'as pas réalisé ? Tu es déjà un Mage, idiot !";
			mes "Un de ces jours, tu réaliseras le pouvoir en toi quand tu pourras créer du feu avec ton esprit !";
		} else
			mes "Hey~ Allez. Arrête de jouer. Tu ne peux pas être un Mage car tu as déjà un autre Job.";
		close;
	} else {
		mes "Hé ?";
		next;
		switch (select("Je veux être un Mage.:Quels sont les prérequis pour être un Mage ?:Rien, merci.")) {
			case 1:
				mes "[Homme de la Guilde des Mages]";
				mes "Veux-tu être un Mage ? Eh...";
				next;
				mes "[Homme de la Guilde des Mages]";
				if (Sex == SEX_MALE) {
					mes "Hé, regarde-toi ! Tu es plutôt mignon~ ! Pas mon type cependant...";
				} else {
					mes "Oooh, tu es une beauté~ !";
					mes "J'aime les filles comme toi~";
				}
				mes "D'accord, tu as dit que tu veux être un Mage ?";
				next;
				if (select("Je veux être un Mage.:Rien, merci.") == 2) {
					mes "[Homme de la Guilde des Mages]";
					mes "Quoiiiii~?! Juste après m'avoir dit que tu veux devenir un Mage, tu changes d'avis ?! Sois un peu plus décidé !";
					close;
				}
				mes "[Homme de la Guilde des Mages]";
				if (!callfunc("F_CanChangeJob")) {
					mes "Oh, mec, ton Niveau de Compétence de Base n'est pas assez élevé pour être un Mage.";
					mes "Retourne et améliore ton Niveau de Compétence de Base.";
					close;
				}
				mes "D'accord.. signe le formulaire de candidature.... Wow~ tu as une belle écriture ! Ahh, d'accord. C'est assez.";
				mes "Tu es... " + strcharinfo(0) + ".";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "Hmm, je vois que tu as fait de ton mieux à ta manière. Bien que ça semble un peu maladroit, eh bien je pense que c'est correct !";
				mes "Bien ! Rester toujours sur les bases est le meilleur ! Je vais te transformer tout de suite.";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "Hahh..! Tu es maintenant un Mage, l'un de nos collègues !";
				mes "Nous te souhaitons la bienvenue à la Guilde des Mages, notre nouvel ami !";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "'Bienvenue à la Guilde des Mages~'";
				mes "Félicitations d'être devenu membre de la Guilde des Mages ! Va de l'avant !";
				callfunc "Job_Change", Job_Mage;
				atcommand "@joblvlup 5";
				getitem 1639, 1; // N_Rod
				close;
			case 2:
				mes "[Homme de la Guilde des Mages]";
				mes "Veux-tu être un Mage, hein ?";
				mes "Je serais heureux de t'expliquer les prérequis pour une jolie fille comme toi !";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "Tout d'abord, tu dois atteindre le Niveau de Job Novice 10 et apprendre toutes les Compétences de Base.";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "Dans le passé, il y avait un test compliqué de fabrication de potion. À cause de cela, nous avons perdu un candidat lentement.";
				mes "Alors, nous avons décidé d'accepter tous les candidats qui remplissent les conditions de base.";
				next;
				mes "[Homme de la Guilde des Mages]";
				mes "N'hésite pas. Sois simplement une magicienne !";
				close;
			case 3:
				mes "[Homme de la Guilde des Mages]";
				mes "Rien... ?";
				close;
		}
	}
}
