//===== rAthena Script ======================================= 
//= Renewal Merchant Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Merchant Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
alberta_in,53,43,6	script	Merchant#mer	86,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator)) {
			mes "[Chef Mahnsoo]";
			mes "Ça fait longtemps !";
			mes "Hé, tu n'as pas arrêté";
			mes "ton commerce, n'est-ce pas ?";
			mes "Qu'est-il arrivé ?";
			next;
			mes "[Chef Mahnsoo]";
			mes "Whoa...";
			mes "Tu es vraiment allé à Valhalla ?! Wow, tu as parcouru un long chemin...";
			next;
			if (!callfunc("F_CanChangeJob")) {
				mes "[Chef Mahnsoo]";
				mes "Hmmm...";
				mes "On dirait que tu n'es pas prêt à redevenir un Marchand. Va finir d'apprendre les Compétences de Base du Novice d'abord.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Ne t'inquiète pas, nous aurons toujours une place de Marchand pour toi. Reviens simplement quand tu seras prêt, d'accord ?";
				close;
			}
			mes "[Chef Mahnsoo]";
			mes "Je suppose que c'est le destin que nous nous rencontrions à nouveau de cette façon. D'accord. Encore une fois, laisse-moi te changer en Marchand !";
			next;
			skill "NV_TRICKDEAD", 0, SKILL_PERM;
			jobchange Job_Merchant_High;
			atcommand "@joblvlup 5";
			skill "MC_CARTREVOLUTION", 1, SKILL_PERM;
			skill "MC_CHANGECART", 1, SKILL_PERM;
			skill "MC_LOUD", 1, SKILL_PERM;
			mes "[Chef Mahnsoo]";
			mes "Ah~ Que de nostalgie. Comme dans le bon vieux temps ! D'accord, fais de ton mieux !";
			close;
		} else {
			mes "[Chef Mahnsoo]";
			mes "^333333*Soupir*^000000";
			mes "Je m'ennuie tellement...";
			mes "Quand vais-je avoir des nouvelles de ma belle Blossom ?";
			close;
		}
	}
	mes "[Guildsman Mahnsoo]";
	mes "Hé, pourquoi es-tu ici ?";
	next;
	switch (select("Je veux être un marchand.:Je veux en savoir plus sur les marchands.:Demande-lui les prérequis pour être un marchand.:Rien.")) {
		case 1:
			mes "[Guildsman Mahnsoo]";
			if (BaseJob != Job_Novice) {
				if (BaseJob == Job_Merchant) {
					mes "..... ? Désolé ? Que dis-tu ?";
					mes "Tu es déjà un marchand. Oh mon...";
					mes "Hein ?! ... Dois-je rire maintenant ?";
				} else {
					mes "Ah ? Tu essaies d'avoir les deux à la fois ?";
					mes "Et si tu te consacrais simplement à ton travail d'origine ?";
					next;
					mes "[Guildsman Mahnsoo]";
					mes "Nous avons des principes éthiques, tu sais.";
				}
				close;
			} else {
				mes "Veux-tu être un marchand ? Hmm...";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Guildsman Mahnsoo]";
					mes "Mais si tu veux être un marchand, ton niveau de compétence de base doit atteindre le niveau 9 ou tu dois dépenser tous tes points de compétence.";
					next;
					mes "[Guildsman Mahnsoo]";
					mes "Ne pensez-vous pas que nous devons apprendre quelques compétences de base même si nous ne traitons qu'avec de l'argent ?";
					close;
				}
				mes "[Guildsman Mahnsoo]";
				mes "Bien, je pense que tu es entièrement prêt pour ça, vu que ton niveau de compétence de base est atteint.";
				mes "Je te permets maintenant d'être un marchand.";
				next;
				mes "[Guildsman Mahnsoo]";
				mes "Félicitations d'être devenu un marchand !";
				mes "Félicitations encore une fois pour être membre de la guilde des marchands et l'un de nos collègues. J'attends ta participation active à partir de maintenant !";
				callfunc "Job_Change", Job_Merchant;
				atcommand "@joblvlup 5";
				getitem 1381, 1; // N_Battle_Axe
				next;
				mes "[Guildsman Mahnsoo]";
				mes "Absolument, nous avons besoin de jeunes gens passionnés pour atteindre notre grand objectif de sécuriser 20% du volume mondial de la monnaie. Tu comprends ? Hein ?";
				mes "Eh bien, je dis juste... cela signifie faisons beaucoup d'argent à la fin. Vous savez ça~";
				close;
			}
		case 2:
			mes "[Guildsman Mahnsoo]";
			mes "Marchand ? Pour le dire simplement, la personne qui vend des biens et gagne de l'argent est un marchand.";
			mes "Pas doué pour se battre et n'a pas de compétences spéciales d'attaque/récupération... mais un marchand peut acheter des choses à bas prix, puis les revendre et gagner de l'argent.";
			next;
			mes "[Guildsman Mahnsoo]";
			mes "Eh bien, un marchand a une compétence ultime appelée Mammonite qui frappe un ennemi avec son argent... Nous pouvons équiper tout sauf les arcs, les baguettes et les épées à deux mains. Mais nous pouvons toujours les vendre et les acheter.";
			mes "Oui... nous, les marchands, avons toujours de l'argent à l'esprit, compris ?";
			close;
		case 3:
			mes "[Guildsman Mahnsoo]";
			mes "Pour devenir un marchand, bien que vendre et recevoir de l'argent soit notre travail, tu dois atteindre au moins le niveau de compétence de base 9.";
			next;
			mes "[Guildsman Mahnsoo]";
			mes "Eh bien, nous recevions un droit d'entrée avant. N'était-ce pas difficile de rassembler cet argent ?";
			mes "Il y avait trop de gens qui ne pouvaient pas rassembler cet argent et qui continuaient à pleurer.";
			next;
			mes "[Guildsman Mahnsoo]";
			mes "Alors récemment, nous avons décidé d'accepter tous les Novices qui veulent devenir marchands.";
			mes "Parce que vivre ou non plus tard dépend entièrement de la capacité de chacun.";
			close;
		case 4:
			close;
	}
}
