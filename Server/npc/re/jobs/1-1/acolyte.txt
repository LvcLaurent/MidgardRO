//===== rAthena Script ======================================= 
//= Renewal Acolyte Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change to Acolyte Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added back the npcs used for the priest quest.
//= 1.2 Added support for Baby classes. [Euphy]
//============================================================ 
prt_church,184,41,4	script	Cleric#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Père Mareusis]";
				mes "Ah, je sens que tu as enduré";
				mes "une expérience de vie passée. Tu as dû apprendre beaucoup de choses avant d'entrer à Valhalla.";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Père Mareusis]";
					mes "Malheureusement, je ne pense pas que tu sois prêt à devenir un Acolyte. Termine d'abord d'apprendre toutes les Compétences de Base.";
					next;
					mes "[Père Mareusis]";
					mes "En attendant,";
					mes "je vais attendre jusqu'à";
					mes "ce que tu sois prêt.";
					mes "Que Dieu soit";
					mes "avec toi.";
					close;
				}
				mes "[Père Mareusis]";
				mes "Bien, je te souhaite la bienvenue";
				mes "de retour de Valhalla et";
				mes "te souhaite bonne chance pour ta";
				mes "nouvelle vie.";
				next;
				skill "NV_TRICKDEAD", 0, SKILL_PERM;
				jobchange Job_Acolyte_High;
				atcommand "@joblvlup 5";
				skill "AL_HOLYLIGHT", 1, SKILL_PERM;
				mes "[Père Mareusis]";
				mes "Maintenant, avance et cherche ceux qui ont besoin de ton aide. Que Dieu éclaire ton chemin.";
				close;
			} else {
				mes "[Père Mareusis]";
				mes "Maintenant, avance pour aider ceux qui ont besoin de toi. Que Dieu illumine ton chemin.";
				close;
			}
		} else {
			mes "[Père Mareusis]";
			mes "Je sens que tu as enduré une expérience de vie passée. Tu as dû apprendre beaucoup de choses avant d'entrer à Valhalla.";
			next;
			mes "[Père Mareusis]";
			mes "Cependant, je peux dire que tu n'es pas fait pour être un Acolyte. Souviens-toi de qui tu étais dans ta vie passée et trouve ton chemin.";
			close;
		}
	}
	mes "[Père Mareusis]";
	mes "Qu'est-ce que tu cherches ?";
	next;
	switch (select("Changer ton job en acolyte.:Demander les prérequis pour être acolyte.:Quitter.")) {
		case 1:
			mes "[Père Mareusis]";
			if (BaseJob != Job_Novice) {
				if (BaseJob == Job_Acolyte)
					mes "Est-ce que ça va aujourd'hui ? Je peux dire par ton accoutrement que tu es déjà un Acolyte. Tu ne plaisantes pas, n'est-ce pas ?";
				else
					mes "Désolé, mais il semble que tu as déjà ton propre job, n'est-ce pas ?";
				close;
			} else {
				mes "Veux-tu vraiment devenir un serviteur de Dieu ?";
				mes "Voyons voir si tu es prêt ou non... Hmm...";
				next;
				if (!callfunc("F_CanChangeJob")) {
					mes "[Père Mareusis]";
					mes "Oh mon Dieu ! Tu n'as pas encore accompli la pratique de base ?! Tu as encore du chemin à faire ! Reviens après avoir augmenté ton niveau de job !";
					close;
				}
				mes "[Père Mareusis]";
				mes "Hmm... ton niveau de job est suffisant...";
				mes "Bien. Maintenant, je vais te donner la qualification pour devenir un Acolyte.";
				next;
				mes "[Père Mareusis]";
				mes "N'oublie jamais d'être reconnaissant envers Dieu, qui veille sur nous tout le temps. Dans le chaos et les moments difficiles, fais face à tes épreuves avec une foi inébranlable.";
				callfunc "Job_Change", Job_Acolyte;
				atcommand "@joblvlup 5";
				getitem 1545, 1; // N_Mace
				next;
				mes "[Père Mareusis]";
				mes "Enfin, je tiens à te féliciter sincèrement d'avoir persévéré dans ton épreuve de pénitence.";
				close;
			}
		case 2:
			mes "[Père Mareusis]";
			mes "Veux-tu devenir un Acolyte ?";
			mes "Alors, tu dois remplir les conditions suivantes en pensant que ce sont les pratiques données par Dieu.";
			next;
			mes "[Père Mareusis]";
			mes "D'abord, tu dois atteindre au moins le niveau 9 du job de Novice et apprendre toutes les Compétences de Base.";
			mes "C'est la chose la plus élémentaire à faire, alors considère-le comme la manière de te former.";
			next;
			mes "[Père Mareusis]";
			mes "Quand tu penseras avoir rempli cette condition, reviens me voir. Alors, tu auras un job sacré où tu pourras répandre la volonté de Dieu.";
			close;
		case 3:
			close;
	}
}

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	mes "[Père Rubalkabara]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Prenez soin de vous. Ils devraient savoir que vous m'avez rencontré d'ici à votre arrivée au Sanctuaire de Prontera.";
			next;
			mes "[Père Rubalkabara]";
			mes "J'ai envoyé un pigeon voyageur avec un message. J'espère qu'il arrivera là-bas en toute sécurité...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Oh... ? Vous devez être celui qui aspire à devenir un Acolyte. J'ai déjà reçu des nouvelles du Sanctuaire selon lesquelles vous pourriez venir.";
				next;
				mes "[Père Rubalkabara]";
				mes "Maintenant, votre nom était " + strcharinfo(0) + ", n'est-ce pas ? Excellent, merci de m'avoir rendu visite.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je suppose que vous avez beaucoup appris sur les Acolytes de Friar Mareusis. De plus, il y a beaucoup de gens serviables au Sanctuaire de Prontera.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je suppose qu'il n'y a vraiment pas besoin que je vous enseigne beaucoup. En outre, je suis sûr que quelqu'un de votre génération pourrait avoir du mal à écouter un vieil homme comme moi. Hahaha~";
				next;
				mes "[Père Rubalkabara]";
				mes "Cependant, les leçons peuvent venir des endroits auxquels on s'attend le moins. Dieu aime enseigner à ses enfants de manière étrange. Vous verrez.";
				next;
				mes "[Père Rubalkabara]";
				mes "Eh bien, je vais envoyer le message disant que vous êtes venu me rendre visite. Vous pouvez maintenant retourner au Sanctuaire de Prontera.";
				next;
				mes "[Père Rubalkabara]";
				mes "Adieu.";
				close2;
				savepoint "prt_fild03", 361, 255, 1, 1;
				set job_acolyte_q, 6;
				end;
			} else {
				mes "Oh...";
				mes "Faites-vous partie des";
				mes "candidats Acolytes... ?";
				mes "Voyons voir...";
				next;
				mes "[Père Rubalkabara]";
				mes "Votre nom est " + strcharinfo(0) + " ?";
				mes "Je ne pense pas que votre nom";
				mes "figure sur ma liste. Hmmm...";
				next;
				mes "[Père Rubalkabara]";
				mes "Pourquoi ne retourneriez-vous pas au Sanctuaire de Prontera et revérifieriez-vous ?";
				close;
			}
		} else {
			mes "Hein ? Qu'est-ce qui vous amène ici ? C'est un endroit très dangereux pour un Novice comme vous !";
			close;
		}
	} else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Salutations.";
			next;
			mes "[Père Rubalkabara]";
			mes "Bienvenue dans les Profondeurs. N'hésitez pas à vous asseoir et à contempler le message de Dieu avec moi. Cet endroit est magnifique, même si le danger accompagne son sentiment de sérénité...";
			close;
		} else {
			mes "Oh ho...";
			mes "Êtes-vous venu ici dans les Profondeurs pour vous entraîner ? Ou êtes-vous simplement un Vagabond ?";
			next;
			mes "[Père Rubalkabara]";
			mes "Qui que vous soyez, prenez bien soin de vous. Les monstres ici sont étonnamment forts, contrairement à leur apparence mignonne.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Ascetic#2aco	95,{
	mes "[Mère Mathilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "Je vais envoyer un pigeon voyageur au Sanctuaire de Prontera. Lorsque vous reviendrez, le Prêtre là-bas devrait déjà avoir reçu mon message.";
			next;
			mes "[Mère Mathilda]";
			mes "Je prierai Dieu et espérerai que vous deveniez bientôt un Acolyte.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Ah, vous devez être l'un des candidats Acolytes. Je vous accueille sincèrement.";
				next;
				mes "[Mère Mathilda]";
				mes "Quel est votre nom ? " + strcharinfo(0) + "? Voyons voir... Ah, vous êtes sur ma liste.";
				next;
				mes "[Mère Mathilda]";
				mes "Je vais envoyer un message au Sanctuaire confirmant que vous, " + strcharinfo(0) + " m'avez rendu visite et avez terminé votre pénitence.";
				next;
				mes "[Mère Mathilda]";
				mes "Veuillez retourner au Sanctuaire de Prontera et parler au Prêtre en charge.";
				close2;
				savepoint "moc_fild07", 35, 355, 1, 1;
				set job_acolyte_q, 7;
				end;
			} else {
				mes "Ah...!";
				mes "Vous devez être l'un";
				mes "des candidats Acolytes.";
				mes "Je vous accueille sincèrement.";
				next;
				mes "[Mère Mathilda]";
				mes "Maintenant, quel est votre nom ?";
				mes "" + strcharinfo(0) + "? Voyons voir...";
				next;
				mes "[Mère Mathilda]";
				mes "Hmm...";
				mes "Il semble que votre nom";
				mes "ne soit pas sur ma liste...";
				next;
				mes "[Mère Mathilda]";
				mes "Peut-être devriez-vous retourner au Sanctuaire de Prontera et vérifier à nouveau la destination de votre épreuve de pénitence.";
				close;
			}
		} else {
			mes "...";
			close;
		}
	} else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Bonjour~";
			next;
			mes "[Mère Mathilda]";
			mes "Comment se déroule votre pratique ? J'espère vraiment que vous appréciez de vivre dans la grâce de Dieu.";
			close;
		} else {
			mes "Que Dieu";
			mes "soit avec vous...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Ascetic#3aco	98,{
	mes "[Père Yosuke]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "Quoi ?";
			next;
			mes "[Père Yosuke]";
			mes "As-tu d'autres affaires avec moi ?! Non ! Retourne au Sanctuaire maintenant !";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Hé.";
				mes "Peu importe ce que tu es,";
				mes "tu ressembles à un";
				mes "candidat Acolyte.";
				mes "Non ?";
				next;
				mes "[Père Yosuke]";
				mes "Pas mal, pas mal. Tu as bien supporté l'épreuve de pénitence.";
				mes "Alors, quel est ton nom ?";
				next;
				mes "[Père Yosuke]";
				mes "" + strcharinfo(0) + ", hein ?";
				next;
				mes "[Père Yosuke]";
				mes "D'accord. Je vais envoyer un message au Sanctuaire indiquant que tu, " + strcharinfo(0) + ", es venu me rendre visite.";
				next;
				mes "[Père Yosuke]";
				mes "Maintenant, retourne au Sanctuaire et termine ta transformation en Acolyte, gamin.";
				close2;
				savepoint "prt_fild00", 206, 230, 1, 1;
				set job_acolyte_q, 8;
				end;
			} else {
				mes "Hé.";
				mes "Tu ressembles à un candidat Acolyte. Ai-je raison ?";
				next;
				mes "[Père Yosuke]";
				mes "Pas mal du tout, tu es venu jusqu'ici depuis Prontera. Alors, quel est ton nom, gamin ?";
				next;
				mes "[Père Yosuke]";
				mes "" + strcharinfo(0) + ", hein ? Pourquoi ton nom ne figure-t-il pas sur ma liste ?";
				next;
				mes "[Père Yosuke]";
				mes "Tu as probablement fait une erreur. Retourne au Sanctuaire et vérifie avec l'Évêque.";
				close;
			}
		} else {
			mes "Toi...";
			mes "Novice.";
			mes "Y a-t-il quelque chose";
			mes "que tu veux me dire ?";
			close;
		}
	} else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hé...";
			next;
			mes "[Père Yosuke]";
			mes "Si tu veux, viens t'asseoir ici avec moi et médite sur les grandes vérités. La majesté de Dieu est vraiment inspirante...";
			close;
		} else {
			mes "As-tu quelque chose à dire ? Parce que malheureusement pour toi,";
			mes "Je ne donne pas de réponses.";
			close;
		}
	}
}

