//===== rAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 3.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=     Removed all functions as they are no longer required.
//=     Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=     scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//= 3.0 Added quest log commands. [L0ne_W0lf]
//= 3.1 Added Izlude Renewal coordinates.
//============================================================ 

valkyrie,48,86,4	script	Valkyrie#fr	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Bienvenue";
		mes "à Valhalla,";
		mes "la Salle de l'Honneur.";
		next;
		mes "[Valkyrie]";
		mes "S'il te plaît,";
		mes "sois à l'aise";
		mes "pendant ton séjour ici.";
		mes "Honneur aux guerriers !";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Bienvenue";
		mes "à Valhalla,";
		mes "la Salle de l'Honneur.";
		next;
		mes "[Valkyrie]";
		mes "Ton moment est venu";
		mes "de mettre fin à ta vie actuelle";
		mes "et de commencer une toute";
		mes "nouvelle vie.";
		mes "Honneur aux guerriers !";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valkyrie]";
			mes "Il y a quelques choses que tu dois";
			mes "faire avant de commencer. Tu dois";
			mes "d'abord vider ton esprit et ton corps.";
			mes "L'honneur vient quand tu abandonnes";
			mes "tous tes désirs égoïstes...";
			next;
			mes "[Valkyrie]";
			mes "Tu ne peux rien emporter avec toi dans la vie suivante. Tes objets, zeny, familiers et chariot doivent tous être laissés derrière.";
			next;
			mes "[Valkyrie]";
			mes "Quand tu seras prêt,";
			mes "reviens vers moi,";
			mes "brave aventurier.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "Je vois que tu as déjà";
			mes "te libérer de toutes";
			mes "tes attaches terrestres,";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[Valkyrie]";
			mes "C'est une attitude admirable pour un aventurier comme toi. L'honneur vient lorsque tu abandonnes tous tes désirs personnels pour le bien de l'humanité.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Hmm... Je sens que tu as";
				mes "quelque attachement persistant ou";
				mes "des affaires inachevées dans ta";
				mes "vie actuelle. Prends-en soin,";
				mes "et mets fin à ta vie actuelle.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Maintenant, laisse-moi effacer";
			mes "tous tes souvenirs actuels...";
			mes "Cependant, tu te souviendras des";
			mes "moments les plus honorables de cette vie.";
			next;
			mes "[Valkyrie]";
			mes "Avec un,";
			mes "je demanderai à la";
			mes "déesse Urd d'effacer";
			mes "tous tes souvenirs actuels.";
			next;
			mes "[Valkyrie]";
			mes "Avec deux,";
			mes "je demanderai à la";
			mes "déesse Verdandi de conserver";
			mes "et d'enregistrer les moments les plus honorables de ta vie actuelle.";
			next;
			mes "[Valkyrie]";
			mes "Avec trois,";
			mes "je demanderai à la";
			mes "déesse Skuld de";
			mes "te guider vers ta";
			mes "prochaine vie.";
			next;
			mes "[Valkyrie]";
			mes "Un...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "Un...";
			mes "Deux......";
			next;
			mes "[Valkyrie]";
			mes "Un...";
			mes "Deux......";
			mes "Et Trois.";
			set ADVJOB, Class+Job_Novice_High; //mémorise l'ID du 3e Job cible
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST & ~1024; //<--réinitialise l'événement de réinitialisation des compétences
			skill "NV_FIRSTAID",1,SKILL_PERM;
			skill "NV_TRICKDEAD",1,SKILL_PERM;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "Félicitations.";
			mes "Tu es maintenant réincarné";
			mes "dans une toute nouvelle vie.";
			mes "Prends ces petits cadeaux";
			mes "en préparation de tes nouvelles aventures.";
			getitem 1202,1; //Couteau_
			getitem 2302,1; //Chemise_en_coton_
			next;
			mes "[Valkyrie]";
			mes "Je souhaite que la libération que la déesse Urd t'a accordée soit une bénédiction. J'espère que les souvenirs que Verdandi a enregistrés t'honoreront toujours.";
			next;
			mes "[Valkyrie]";
			mes "Et je prie pour que la nouvelle vie vers laquelle la déesse Skuld te guidera soit encore plus honorable que la précédente.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (checkre(0)) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Bienvenue";
		mes "à Valhalla,";
		mes "la Salle de l'Honneur.";
		next;
		mes "[Valkyrie]";
		mes "Malheureusement, tu n'as pas encore";
		mes "été invité ici. Je te demande de partir";
		mes "immédiatement. Honneur aux guerriers !";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Metheus Sylphe#Library	742,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Metheus Sylphe]";
			mes "Bienvenue à la Bibliothèque de l'Académie de Magie de Schweicherbil.";
			mes "Je suppose que vous êtes venu ici";
			mes "pour lire le 'Livre d'Ymir.'";
			next;
			mes "[Metheus Sylphe]";
			mes "Malheureusement, la copie originale du livre a été endommagée au fil du temps. Nous autorisons actuellement le public à consulter une copie du livre.";
			next;
			mes "[Metheus Sylphe]";
			mes "De plus, afin de préserver le 'Livre d'Ymir' original, nous avons décidé d'accepter des dons de personnes souhaitant lire la copie que nous avons fournie.";
			next;
			mes "[Metheus Sylphe]";
			mes "Le montant de donation suggéré est";
			mes "1 285 000 zeny.";
			next;
			if (select("Faire un don.:Annuler.") == 1) {
				if (Zeny >= 1285000) {
					set Zeny, Zeny-1285000;
					set valkyrie_Q,1;
					mes "[Metheus Sylphe]";
					mes "Merci, votre don sera utilisé à bon escient. Vous pouvez";
					mes "maintenant entrer et lire le livre.";
					close;
				}
				mes "[Metheus Sylphe]";
				mes "Malheureusement, vous ne semblez pas posséder suffisamment de zeny pour le moment. Veuillez vérifier vos fonds et revenir plus tard.";
				close;
			}
			mes "[Metheus Sylphe]";
			mes "Prenez votre temps, et";
			mes "profitez de vos voyages.";
			close;
		}
		mes "[Metheus Sylphe]";
		mes "Encore une fois, merci pour votre généreuse donation. N'hésitez pas à lire une copie carbone du 'Livre d'Ymir' à votre guise.";
		close;
	}
	mes "[Metheus Sylphe]";
	mes "Bienvenue à la Bibliothèque de l'Académie de Magie de Schweicherbil.";
	mes "Ici, nous avons un nombre incalculable de livres. Prenez votre temps et regardez autour de vous.";
	close;
}

yuno_in02,93,207,1	script	Book of Ymir	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Le Livre d'Ymir]";
		mes "...L'entrée du Hall d'Honneur est ouverte à quiconque a progressé dans sa vie suivante. Elle est là pour aider les héros à décider de ce qu'ils veulent faire et peut les mener n'importe où dans ce monde.";
		next;
		mes "[Le Livre d'Ymir]";
		mes "Dans le Hall d'Honneur, tout est préparé pour les héros. On dit que tout souhait qui ne peut être réalisé dans notre réalité peut être exaucé dans le Hall d'Honneur.";
		next;
		if (select("Arrêter de lire.:Continuer à lire.") == 1) {
			mes "[Le Livre d'Ymir]";
			mes ".....";
			close;
		}
		mes "[Le Livre d'Ymir]";
		mes "Il existe un chemin oublié qui mène au Hall d'Honneur, l'endroit le plus proche du ciel. Les gens ordinaires ne découvriront jamais cet endroit...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[Le Livre d'Ymir]";
			mes "...C'est pourquoi, les anciens héros étaient";
			mes "toujours tourmentés, sachant qu'à la fin,";
			mes "ils étaient mortels et";
			mes "passeraient de ce royaume...";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Il n'y avait aucun document,";
			mes "chanson, ou folklore restant qui contenait des informations sur la vie après la mort. Cependant, j'ai récemment découvert un vieux parchemin";
			mes "sur les Valkyries...";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Valkyrie...";
			mes "La légendaire";
			mes "ange gardien.";
			mes "Ange de Ragnarok.";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Les aventuriers d'une grande force";
			mes "et bravoure seront guidés par";
			mes "Valkyrie vers Valhalla, le Hall";
			mes "d'Honneur. Là, ils auront une";
			mes "nouvelle vie.";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Renaissants, ils vivront à nouveau";
			mes "en tant que héros encore plus grands";
			mes "qui éclaireront le monde. Les corps";
			mes "épuisés seront remplis d'énergie...";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Et leurs âmes se verront conférer des capacités avec le cœur d'Ymir. Cependant, le cœur d'Ymir a été totalement détruit et dispersé partout dans le monde après la bataille pour Rune-Midgarts.";
			next;
			mes "[Le Livre d'Ymir]";
			mes "J'ai trouvé une petite quantité";
			mes "de morceaux du cœur d'Ymir sur";
			mes "une très longue période. Mais je ne peux pas";
			mes "confirmer si l'histoire de Valkyrie";
			mes "et de Valhalla est vraie juste";
			mes "à travers des tests scientifiques.";
			next;
			mes "[Le Livre d'Ymir]";
			mes "Ainsi, je laisse cet enregistrement dans l'espoir que quelqu'un, à l'avenir,";
			mes "pourra confirmer que Valkyrie et Valhalla existent réellement...";
			next;
			set valkyrie_Q,2;
			if(checkquest(1000) == -1) {
				setquest 1000;
			}
			mes "[Le Livre d'Ymir]";
			mes "Que les héros vivent de nouvelles vies";
			mes "afin de protéger le monde";
			mes "du danger. Et ensuite...";
			close;
		}
		mes "[Le Livre d'Ymir]";
		mes "...";
		close;
	}
	mes "[Le Livre d'Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	111,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Téléporteur]";
			mes "Félicitations.";
			mes "Honneur aux guerriers !";
			close;
		}
		mes "[Téléporteur]";
		mes "Veuillez vous abstenir";
		mes "de toucher aux expositions.";
		mes "..........";
		close;
	}
	else {
		mes "[Téléporteur]";
		mes "Honoré,";
		mes "vers quel endroit souhaitez-vous aller ?";
		next;
		switch(select("Prontera:Morocc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (checkre(0)) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
	
S_WarpChar:
	mes "[Téléporteur]";
	mes "Bon voyage.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2),1,1;
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

